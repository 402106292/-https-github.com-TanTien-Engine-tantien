import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for VAR_TYPE_ARRAY, VAR_TYPE_NUMBER3
import "blueprint.blueprint" for Blueprint
import "blueprint.variant" for Variant
import "scenegraph.variant" for VAR_TYPE_SPRITE, VarSprite
import "maths" for Matrix44

class Transform3d is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "spr",       [ VAR_TYPE_SPRITE, VAR_TYPE_ARRAY ]),
			Pin(this, "translate", VAR_TYPE_NUMBER3),
			Pin(this, "rotate",    VAR_TYPE_NUMBER3),
			Pin(this, "scale",     VAR_TYPE_NUMBER3),
		]
		this.exports = [
			Pin(this, "spr", [ VAR_TYPE_SPRITE, VAR_TYPE_ARRAY ])
		]

		this.layout()
	}

	on_pin_dirty(pin)
	{
		if (pin.is_input) {
			Blueprint.send_pin_dirty(this.exports[0])
		}
	}

	calc_value(idx)
	{
		var v_spr = Blueprint.calc_input_value(this, 0)
		if (!v_spr) {
			return nil
		}

		var result = []
		this.transform_impl(v_spr, result)
		if (result.count == 0) {
			return nil
		} else if (result.count == 1) {
			return result[0]
		} else {
			return Variant(result)
		}
	}

	transform_impl(v_src, result)
	{
		if (v_src.type == VAR_TYPE_ARRAY) {
			for (var v in v_src.value) {
				this.transform_impl(v, result)
			}
		} else if (v_src.type == VAR_TYPE_SPRITE) {
			result.add(this.transform_spr(v_src))
		}
	}

	transform_spr(v_spr)
	{
		// scale
		var sx = 1
		var sy = 1
		var sz = 1
		var v_scale = Blueprint.calc_input_value(this, 3)
		if (v_scale) {
			sx = v_scale.value.x
			sy = v_scale.value.y
			sz = v_scale.value.z
		}

		var scale_mat = Matrix44.init()
		scale_mat.scale(sx, sy, sz)		

		// translate
		var x = 0
		var y = 0
		var z = 0
		var v_translate = Blueprint.calc_input_value(this, 1)
		if (v_translate) {
			x = v_translate.value.x
			y = v_translate.value.y
			z = v_translate.value.z
		}

		var trans_mat = Matrix44.init()
		trans_mat.translate(x, y, z)

		// rotate
		var rx = 0
		var ry = 0
		var rz = 0
		var v_rotate = Blueprint.calc_input_value(this, 2)
		if (v_rotate) {
			rx = v_rotate.value.x
			ry = v_rotate.value.y
			rz = v_rotate.value.z
		}

		var rot_mat = Matrix44.init()
		rot_mat.rotate(rx, ry, rz)

		// apply to new spr
		var spr = v_spr.value.clone()

		if (!spr.transform) {
			spr.transform = Matrix44.init()
		}
		spr.transform.transform_mat4(scale_mat)
		spr.transform.transform_mat4(rot_mat)
		spr.transform.transform_mat4(trans_mat)

		return Variant(VAR_TYPE_SPRITE, spr)
	}
}