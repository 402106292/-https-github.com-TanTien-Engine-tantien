import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for Variant, VAR_TYPE_ARRAY
import "blueprint.blueprint" for Blueprint
import "scenegraph.variant" for VAR_TYPE_SPRITE, VarSprite

class Combine is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "child0", VAR_TYPE_SPRITE),
			Pin(this, "child1", VAR_TYPE_SPRITE),
		]
		this.exports = [
			Pin(this, "parent", VAR_TYPE_SPRITE),
		]

		this.layout()

		this.spr = nil
	}

	on_pin_dirty(pin)
	{
		if (pin.is_input) {
			Blueprint.send_pin_dirty(this.exports[0])
		}

		this.spr = nil
	}

	calc_value(idx)
	{
		if (!this.spr)
		{
			this.spr = VarSprite()
			for (var i in 0..this.imports.count) {
				var v_child = Blueprint.calc_input_value(this, i)
				this.add_child(this.spr, v_child)
			}
		}
		return Variant(VAR_TYPE_SPRITE, this.spr)
	}

	add_child(spr, v_child)
	{
		if (!v_child) {
			return
		}

		if (v_child.type == VAR_TYPE_ARRAY) {
			for (var v in v_child.value) {
				this.add_child(spr, v)
			}
		} else {
			spr.children.add(v_child.value)
		}
	}

	on_connected(pin)
	{
		if (pin.is_input) {
			this.update_imports()
		}
	}

	on_disconnected(pin)
	{
		if (pin.is_input) {
			this.update_imports()
		}
	}

	update_imports()
	{
		while (this.imports.count > 2 and this.imports.back().conns.isEmpty) {
			this.imports.pop_back()
		}

		if (!this.imports.back().conns.isEmpty) {
			this.imports.add(Pin(this, "child%(this.imports.count)", VAR_TYPE_SPRITE))
		}

		this.layout()
	}
}