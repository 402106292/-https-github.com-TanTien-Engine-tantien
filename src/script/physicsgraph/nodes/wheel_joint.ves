import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for Variant, VAR_TYPE_NUMBER, VAR_TYPE_NUMBER2
import "blueprint.node_param" for NodeParam
import "blueprint.blueprint" for Blueprint
import "physicsgraph.variant" for VAR_TYPE_BODY, VAR_TYPE_JOINT, VarJoint
import "maths.vector" for Vector2
import "physics" for WheelJoint as tt_WheelJoint

var EDITOR = nil

class WheelJoint is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "body_a",    VAR_TYPE_BODY),
			Pin(this, "body_b",    VAR_TYPE_BODY),
			Pin(this, "anchor",    VAR_TYPE_NUMBER2),			
			Pin(this, "stiffness", VAR_TYPE_NUMBER),
			Pin(this, "damping",   VAR_TYPE_NUMBER),			
		]
		this.exports = [
			Pin(this, "joint", VAR_TYPE_JOINT),
		]

		this.params.add(NodeParam("axis", Vector2(0, 1)))
		// translate_limit
		this.params.add(NodeParam("limit", true))
		this.params.add(NodeParam("lower", -0.25))
		this.params.add(NodeParam("upper", 0.25))
		// motor
		this.params.add(NodeParam("motor", false))		
		this.params.add(NodeParam("torque", 10.0, { "region" : 100 }))
		this.params.add(NodeParam("speed", 0.0))

		this.layout()

		this.joint = VarJoint()
	}
	
	calc_value(idx)
	{
		if (!this.joint.joint) {
			this.joint.joint = this.build_joint()
		}
		return Variant(VAR_TYPE_JOINT, this.joint)
	}

	build_joint()
	{
		var v_a = Blueprint.calc_input_value(this, 0)
		var v_b = Blueprint.calc_input_value(this, 1)
		if (!v_a or !v_b) {
			return nil
		}

		var anchor = nil
		var v_anchor = Blueprint.calc_input_value(this, 2)
		if (v_anchor) {
			anchor = v_anchor.value
		} else {
			anchor = Vector2(0, 0)
		}
		var axis  = this.query_param("axis").value
		var joint = tt_WheelJoint.init(v_a.value.body, v_b.value.body, [anchor.x, anchor.y], [axis.x, axis.y])

		var stiffness = 0
		var v_stiffness = Blueprint.calc_input_value(this, 3)
		if (v_stiffness) {
			stiffness = v_stiffness.value
		}
		var damping = 0
		var v_damping = Blueprint.calc_input_value(this, 4)
		if (v_damping) {
			damping = v_damping.value
		}
		joint.set_suspension(stiffness, damping)

		var limit = this.query_param("limit").value
		var lower = this.query_param("lower").value
		var upper = this.query_param("upper").value
		joint.set_translate_limit(limit, lower, upper)

		var motor  = this.query_param("motor").value
		var torque = this.query_param("torque").value
		var speed  = this.query_param("speed").value	
		joint.set_motor(motor, torque, speed)

		return joint
	}
}