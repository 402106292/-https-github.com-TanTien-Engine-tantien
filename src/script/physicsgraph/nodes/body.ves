import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for Variant, VAR_TYPE_ARRAY, VAR_TYPE_INTEGER, VAR_TYPE_NUMBER2
import "blueprint.blueprint" for Blueprint
import "blueprint.node_param" for NodeParam
import "geograph.variant" for VarGeo, VAR_TYPE_GEO
import "physicsgraph.variant" for VAR_TYPE_BODY, VAR_TYPE_FIXTURE, VarBody, VarFixture
import "scenegraph.variant" for VarSprite, VAR_TYPE_SPRITE
import "physics" for Body as tt_Body
import "maths" for Matrix2D
import "geometry" for Rect

class Body is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "geos", [ VAR_TYPE_GEO, VAR_TYPE_SPRITE, VAR_TYPE_FIXTURE, VAR_TYPE_ARRAY ]),
			Pin(this, "flag", VAR_TYPE_INTEGER),
		]
		this.exports = [
			Pin(this, "body",   VAR_TYPE_BODY),
			Pin(this, "bodies", VAR_TYPE_ARRAY),
		]

		this.params.add(NodeParam("type", "static", { "combo" : [ "static", "kinematic", "dynamic" ] }))

		this.params.add(NodeParam("gravity",     1.0, { "min" : 0.0, "max" : 2.0 }))
		// todo: need fixture?
		this.params.add(NodeParam("density",     1.0, { "min" : 0.0, "max" : 2.0 }))
		this.params.add(NodeParam("restitution", 0.0, { "min" : 0.0, "max" : 2.0 }))
		this.params.add(NodeParam("friction",    0.2, { "min" : 0.0, "max" : 1.0 }))

		this.layout()
	}

	calc_value(idx)
	{
		if (idx == 0)
		{
			var v_geos = Blueprint.calc_input_value(this, 0)
			return Variant(VAR_TYPE_BODY, this.build_body(v_geos))
		}
		else if (idx == 1)
		{
			var bodies = []
			var v_geos = Blueprint.calc_input_value(this, 0)
			this.build_body_single(v_geos, bodies)

			return Variant(bodies)
		}

		return nil
	}

	build_body_single(v_geos, bodies)
	{
		if (v_geos.type == VAR_TYPE_ARRAY) {
			for (var v_geo in v_geos.value) {
				this.build_body_single(v_geo, bodies)
			}
		} else {
			bodies.add(Variant(VAR_TYPE_BODY, this.build_body(v_geos)))
		}
	}

	build_body(v_geos)
	{
		var flag = nil
		var v_flag = Blueprint.calc_input_value(this, 1)
		if (v_flag) {
			flag = v_flag.value
		}

		var type = this.query_param("type").value
		var body = tt_Body.init(type, flag)

		var mat = Matrix2D.init()
		this.build_body(v_geos, body, mat, [])

		var v_body = VarBody()

		if (v_geos.type == VAR_TYPE_SPRITE and v_geos.value.transform) 
		{
			var srt = v_geos.value.transform.decompose()
			v_body.scale = [srt[0], srt[1]]
			body.set_transform([srt[3], srt[4]], srt[2])
		}

		body.set_gravity_scale(this.query_param("gravity").value)
		body.set_density(this.query_param("density").value)
		body.set_restitution(this.query_param("restitution").value)
		body.set_friction(this.query_param("friction").value)

		v_body.body = body
		if (v_geos) {
			if (v_geos.type == VAR_TYPE_SPRITE) {
				v_body.spr = v_geos.value			
			} else if (v_geos.type == VAR_TYPE_FIXTURE) {
				v_body.spr = v_geos.value.shape.value
			}
		} else {
			v_body.geo = v_geos
		}

		return v_body
	}

	build_body(v_geo, body, mat, filter)
	{
		if (v_geo.type == VAR_TYPE_ARRAY)
		{
			for (var geo in v_geo.value) {
				this.build_body(geo, body, mat, filter)
			}
		}
		else if (v_geo.type == VAR_TYPE_GEO)
		{
			if (v_geo.value.shape) {
				var fill = v_geo.value.render_style["fill"]
				body.add_fixture(v_geo.value.shape, fill, mat, filter)
			}
		}
		else if (v_geo.type == VAR_TYPE_SPRITE)
		{
			var spr = v_geo.value
			if (spr.symbol) 
			{
				if (spr.symbol is VarGeo) 
				{
					var fill = spr.symbol.render_style["fill"]
					body.add_fixture(spr.symbol.shape, fill, mat, filter)
				} 
				else if (spr.symbol is VarFixture) 
				{
					var c_filter = this.calc_filter(spr.symbol)
					this.build_body(spr.symbol.shape, body, mat, c_filter)
				}
				else if (spr.size) 
				{
					var w = spr.size.x
					var h = spr.size.y

					var rect = Rect.init()
					rect.set(-w * 0.5, -h * 0.5, w, h)
					body.add_fixture(rect, true, mat, filter)
				}
			} 
			else 
			{
				for (var child in spr.children) {
					var c_mat = this.calc_mat(child, mat)
					this.build_body(Variant(VAR_TYPE_SPRITE, child), body, c_mat, filter)
				}
			}
		}
		else if (v_geo.type == VAR_TYPE_FIXTURE)
		{
			var fixture = v_geo.value
			var filter = this.calc_filter(fixture)
			this.build_body(fixture.shape, body, mat, filter)
		}
	}

	calc_filter(fixture)
	{
		var filter = []
		if (fixture.category) {
			filter.add(fixture.category)
		} else {
			filter.add(0)		
		}

		if (fixture.not_collide) {
			for (var id in fixture.not_collide) {
				filter.add(id)
			}
		}

		return filter
	}

	calc_mat(spr, mat)
	{
		var ret = Matrix2D.init()
		if (spr.transform) {
			ret.transform_mat(spr.transform)
		}
		if (mat) {
			ret.transform_mat(mat)
		}
		return ret
	}
}