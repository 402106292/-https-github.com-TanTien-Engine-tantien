import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for Variant, VAR_TYPE_ARRAY
import "blueprint.node_param" for NodeParam
import "blueprint.blueprint" for Blueprint
import "geograph.variant" for VAR_TYPE_GEO, VarGeo
import "geograph.brush" for Brush
import "geometry" for Polytope

class Boolean is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "a", [ VAR_TYPE_GEO, VAR_TYPE_ARRAY ]),
			Pin(this, "b", [ VAR_TYPE_GEO, VAR_TYPE_ARRAY ]),
		]
		this.exports = [
			Pin(this, "geo", [ VAR_TYPE_GEO, VAR_TYPE_ARRAY ]),
		]

		this.params.add(NodeParam("operator", "intersect", { "combo" : [ "union", "intersect", "subtract" ] }))

		this.layout()
	}

	on_pin_dirty(pin)
	{
		if (pin.is_input) {
			Blueprint.send_pin_dirty(this.exports[0])
		}
	}	

	calc_value(idx)
	{
		var v_a = Blueprint.calc_input_value(this, 0)
		if (!v_a) {
			return nil
		}
		var v_b = Blueprint.calc_input_value(this, 1)
		if (!v_b) {
			return nil
		}

		var b = []
		this.get_polys(b, v_b)

		var operator = this.query_param("operator").value

		var brushes = []
		this.boolean_impl(brushes, v_a, b, operator)

		if (brushes.isEmpty) {
			return nil
		} else if (brushes.count == 1) {
			return brushes[0]
		} else {
			return Variant(brushes)
		}
	}	

	get_polys(dst, src)
	{
		if (src.type == VAR_TYPE_ARRAY) {
			for (var v in src.value) {
				this.get_polys(dst, v)
			}
		} else if (src.type == VAR_TYPE_GEO and src.value.shape is Brush) {
			for (var p in src.value.shape.polys) {
				dst.add(p)
			}
		}
	}

	boolean_impl(dst, v_a, b, op)
	{
		if (v_a.type == VAR_TYPE_ARRAY)
		{
			var list = []

			for (var item in v_a.value) {
				this.boolean_impl(list, item, b, op)
			}

			if (!list.isEmpty) 
			{
				if (list.count == 1) {
					dst.add(list[0])
				} else {
					dst.add(Variant(list))
				}
			}
		}
		else
		{
			var a = []
			this.get_polys(a, v_a)

			var d = this.boolean_impl(a, b, op)
			if (d) {
				dst.add(d)
			}			
		}
	}

	boolean_impl(a, b, op)
	{
		var polys = Polytope.boolean(op, a, b)
		if (polys.isEmpty) {
			return nil
		} else {
			var geo = VarGeo()
			geo.shape = Brush(polys)
			return Variant(VAR_TYPE_GEO, geo)
		}
	}
}