import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for Variant, VAR_TYPE_ARRAY, VAR_TYPE_NUMBER2
import "blueprint.node_param" for NodeParam
import "blueprint.blueprint" for Blueprint
import "geograph.variant" for VAR_TYPE_GEO, VarGeo
import "geometry" for Polyline as tt_Polyline

class Polyline is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "vertices", VAR_TYPE_ARRAY),
		]
		this.exports = [
			Pin(this, "geo", VAR_TYPE_GEO),
		]

		this.params.add(NodeParam("vertices", []))

		this.layout()

		this.geo = nil
		this.geo_dirty = false
	}

	on_pin_dirty(pin)
	{
		this.geo_dirty = true
	}

	on_node_dirty()
	{
		this.geo_dirty = true
	}
	
	calc_value(idx)
	{
		var geo = this.get_geo()
		return Variant(VAR_TYPE_GEO, geo)
	}

	on_gui_dirty() 
	{
		this.geo_dirty = true
	}

	geo_solver_gen(scene)
	{
		var geo = this.get_geo()
		geo.geo_id = scene.add_geo(geo.shape)
	}

	get_geo()
	{
		if (!this.geo) 
		{
			this.geo = VarGeo()
			this.geo.shape = tt_Polyline.init()
			this.update_geo(this.geo)
		}
		else if (this.geo_dirty)
		{
			this.update_geo(this.geo)
		}

		return this.geo
	}

	update_geo(geo)
	{
		var v_verts = Blueprint.calc_input_value(this, 0)
		if (v_verts)
		{
			if (v_verts.type != VAR_TYPE_ARRAY or v_verts.value.isEmpty) {
				return
			}

			var type = v_verts.value[0].type
			if (type == VAR_TYPE_NUMBER2)
			{
				var vertices = []
				for (var vert in v_verts.value) {
					vertices.add(vert.value.x)
					vertices.add(vert.value.y)
				}
				geo.shape.set_vertices(vertices)
			}
			else if (type == VAR_TYPE_GEO)
			{
				var vertices = []
				for (var shape in v_verts.value) 
				{
					var circle = shape.value.shape.get()
					vertices.add(circle[0])
					vertices.add(circle[1])
				}
				geo.shape.set_vertices(vertices)
			}
		}
		else
		{
			var vertices = this.query_param("vertices").value
			geo.shape.set_vertices(vertices)
		}

		this.geo_dirty = false
	}
}