import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for Variant, VAR_TYPE_NUMBER2
import "blueprint.node_param" for NodeParam
import "blueprint.blueprint" for Blueprint
import "geograph.variant" for VAR_TYPE_GEO, VarGeo
import "geometry" for Rect as tt_Rect
import "maths.vector" for Vector3

class Rect is Node
{
	init()
	{
		super.init()

		this.imports = [
			Pin(this, "pos", VAR_TYPE_NUMBER2),
			Pin(this, "size", VAR_TYPE_NUMBER2),			
		]
		this.exports = [
			Pin(this, "geo", VAR_TYPE_GEO),
			Pin(this, "new_geo", VAR_TYPE_GEO),
		]

		// shape
		this.params.add(NodeParam("x", 0, { "min" : -1000, "max" : 1000 }))
		this.params.add(NodeParam("y", 0, { "min" : -1000, "max" : 1000 }))
		this.params.add(NodeParam("w", 100, { "region" : 1000 }))
		this.params.add(NodeParam("h", 100, { "region" : 1000 }))

		// draw
		this.params.add(NodeParam("fill", false))
		this.params.add(NodeParam("color", Vector3(1.0, 0.0, 0.0)))

		this.layout()

		this.geo = nil
	}

	calc_value(idx)
	{
		if (idx == 0)
		{
			var geo = this.get_geo()
			return Variant(VAR_TYPE_GEO, geo)
		}
		else if (idx == 1)
		{
			var geo = VarGeo()
			geo.shape = tt_Rect.init()
			this.update_geo(geo)
			return Variant(VAR_TYPE_GEO, geo)
		}
		return nil
	}

	on_gui_dirty() 
	{
		var geo = this.get_geo()
		this.update_geo(geo)
	}

	geo_solver_gen(scene)
	{
		var geo = this.get_geo()
		geo.geo_id = scene.add_geo(geo.shape)
	}

	get_geo()
	{
		if (!this.geo) 
		{
			this.geo = VarGeo()
			this.geo.shape = tt_Rect.init()
			this.update_geo(this.geo)
		}

		return this.geo
	}

	update_geo(geo)
	{
		var x = 0
		var y = 0
		var v_pos = Blueprint.calc_input_value(this, 0)
		if (v_pos) {
			x = v_pos.value.x
			y = v_pos.value.y
		} else {
			x = this.query_param("x").value
			y = this.query_param("y").value
		}

		var w = 0
		var h = 0
		var v_size = Blueprint.calc_input_value(this, 1)
		if (v_size) {
			w = v_size.value.x
			h = v_size.value.y
		} else {
			w = this.query_param("w").value
			h = this.query_param("h").value		
		}
		
		geo.shape.set(x, y, w, h)

		geo.render_style["fill"] = this.query_param("fill").value
		geo.render_style["color"] = this.query_param("color").value
	}
}