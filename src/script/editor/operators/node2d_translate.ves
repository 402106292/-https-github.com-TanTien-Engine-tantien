import "editor.editop" for EditOP
import "maths.vector" for Vector2
import "editor.message" for MSG_SELECTION_MOVED, MSG_SELECTION_MOVING

class Node2dTranslate is EditOP
{
	init(cam, scene_stack)
	{
		this.camera = cam
		this.scene_stack = scene_stack

		this.last_pos = nil
	}

	on_mouse_press(x, y)
	{
		this.last_pos = Vector2(x, y)
	}

	on_mouse_release(x, y)
	{
		this.last_pos = nil

		var scene = this.scene_stack.top()
		if (!scene.selection.isEmpty) {
			var p = this.camera.screen2world(Vector2(x, y))
			this.scene_stack.send_message(scene, MSG_SELECTION_MOVED, [p])
		}
	}

	on_mouse_drag(x, y)
	{
		if (this.last_pos.x == x and this.last_pos.y == y) {
			return
		}

		var p0 = this.camera.screen2world(this.last_pos)
		var p1 = this.camera.screen2world(Vector2(x, y))
 		this.translate(Vector2(p1.x - p0.x, p1.y - p0.y))

		this.last_pos.set(x, y)
	}

	translate(offset)
	{
		var scene = this.scene_stack.top()

		for (var node in scene.selection)
		{
			var bb = node.components["bounding"]
			if (bb) {
				bb.translate(offset)
			}

			var trans = node.components["transform"]
			if (trans) {
				trans.translate(offset)
			}
		}

		if (!scene.selection.isEmpty) {
			this.scene_stack.send_message(scene, MSG_SELECTION_MOVING, [offset])
		}
	}
}