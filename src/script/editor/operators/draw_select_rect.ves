import "editor.editop" for EditOP
import "editor.colors" for Colors
import "graphics" for Painter, Graphics
import "maths.vector" for Vector2
import "maths.rect" for Rect

class DrawSelectRect is EditOP
{
	init(cam, scene) 
	{
		this.camera = cam

		this.first_pos = nil
		this.last_pos = nil
	}

	on_mouse_press(x, y)
	{
		this.first_pos = Vector2(x, y)
		this.last_pos = Vector2(x, y)
	}

	on_mouse_release(x, y)
	{
		this.first_pos = nil
		this.last_pos = nil
	}

	on_mouse_drag(x, y)
	{
		if (this.first_pos) {
			this.last_pos.set(x, y)
		}
	}

	draw()
	{
		if (!this.first_pos or !this.last_pos) {
			return
		}

		var pt = Painter.init()

		var p0 = this.camera.screen2project(this.first_pos)
		var p1 = this.camera.screen2project(this.last_pos)

		var color
		if (p1.x > p0.x) {
			color = Colors.light_green()
		} else {
			color = Colors.light_blue()
		}

		var r = Rect(p0, p1)
		pt.add_rect_filled([r.x, r.y, r.w, r.h], color)
		pt.add_rect([r.x, r.y, r.w, r.h], Colors.select_red(), 2 * this.camera.scale)

		Graphics.draw_painter(pt)
	}

	clear()
	{
		this.first_pos = nil
		this.last_pos = nil
	}
}