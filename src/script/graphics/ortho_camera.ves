import "maths.vector" for Vector2
import "graphics" for Graphics

class OrthoCamera
{
	init() 
	{
		this.pos = Vector2()
		this.scale = 1.0

		this.size = Vector2()
	}

	on_size(w, h)
	{
		this.size.set(w, h)
	}

	screen2project(screen) 
	{
		var proj = Vector2()
		proj.x = (screen.x - this.size.x * 0.5)  * this.scale + this.pos.x
		proj.y = (this.size.y * 0.5 - screen.y) * this.scale + this.pos.y
		return proj
	}

	project2screen(project)
	{
		var screen = Vector2()
		screen.x = (project.x - this.pos.x) / this.scale + this.size.x * 0.5
		screen.y = this.size.y * 0.5 - (project.y - this.pos.y) / this.scale
		return screen
	}

	translate_cam(offset)
	{
		this.pos.x = this.pos.x + offset.x * this.scale
		this.pos.y = this.pos.y + offset.y * this.scale

		Graphics.onCamUpdate(this.pos.x, this.pos.y, this.scale)	
	}

	scale_cam(scale, pos)
	{
		var x = pos.x
		var y = this.size.y - pos.y
		var hw = this.size.x * 0.5
		var hh = this.size.y * 0.5
		var new_scale = this.scale * scale
		this.pos.x = (x - hw) * this.scale + this.pos.x - (x - hw) * new_scale
		this.pos.y = (y - hh) * this.scale + this.pos.y - (y - hh) * new_scale
		this.scale = new_scale

		Graphics.onCamUpdate(this.pos.x, this.pos.y, this.scale)
	}
}