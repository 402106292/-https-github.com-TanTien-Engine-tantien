import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for Variant
import "blueprint.node_helper" for NodeHelper
import "blueprint.node_layout" for DEFAULT_HEIGHT
import "rendergraph.variant" for VAR_TYPE_SHADER
import "rendergraph.variates.shader" for Shader as var_Shader
import "rendergraph.components.shader_uniforms" for ShaderUniforms
import "gui" for GUI
import "render" for Shader as tt_Shader

class Shader is Node
{
	init() 
	{
		super.init(Shader.name)

		this.exports = [
			Pin(this, "out", VAR_TYPE_SHADER)
		]

		this.vs  = ""
		this.tcs = ""
		this.tes = ""		
		this.gs  = ""
		this.fs  = ""
		this.cs  = ""

		this.shader = var_Shader(this)
		this.dirty = true

		this.uniforms = []

		this.layout()
	}

	calc_value(idx)
	{
		return Variant(VAR_TYPE_SHADER, this.shader)
	}

	get_shader_uniforms()
	{
		var uniforms = []
		ShaderUniforms.get_shader_uniforms("vertex",    this.vs,  uniforms)
		ShaderUniforms.get_shader_uniforms("tess_ctrl", this.tcs, uniforms)
		ShaderUniforms.get_shader_uniforms("tess_eval", this.tes, uniforms)
		ShaderUniforms.get_shader_uniforms("geometry",  this.gs,  uniforms)
		ShaderUniforms.get_shader_uniforms("pixel",     this.fs,  uniforms)
		ShaderUniforms.get_shader_uniforms("compute",   this.cs,  uniforms)		
		return uniforms
	}

	execute() 
	{
		if (!this.dirty) {
			return
		}

		this.shader.prog = nil
		if (this.cs != "") {
			this.shader.prog = tt_Shader.init(this.cs)
		} else {
			this.shader.prog = tt_Shader.init(this.vs, this.tcs, this.tes, this.gs, this.fs)
		}

		if (this.shader.prog)
		{
			this.uniforms = this.get_shader_uniforms()
			for (var unif in this.uniforms) {
				//System.print("+ %(unif[0])")
				ShaderUniforms.add_uniform_to_imports(this, unif)
			}

			this.layout()
		}

		this.dirty = false
	}

	layout() 
	{
		super.layout()

		this.style.width = 180

		for (var unif in this.uniforms) {
			ShaderUniforms.layout(this, unif)
		}
	}

	set_uniform(unif, name, value)
	{
		if (unif[1] == "array" or unif[1] == "struct")
		{
			for (var u in unif[2]) {
				this.set_uniform(u, name, value)
			}		
		}
		else if (unif[0] == name)
		{
			for (var i in 0..value.count) {
				unif[2][i] = value[i]
			}
		}
	}

	set_uniform(name, value)
	{
		for (var unif in this.uniforms) {
			this.set_uniform(unif, name, value)
		}
	}

	uniform_to_string(name, unif)
	{
		var ret = ""

		for (var p in this.imports) {
			if (p.name == unif[0]) {
				if (!p.conns.isEmpty) {
					return ret
				}
			}
		}

		if (unif[1] == "array" or unif[1] == "struct") 
		{
			for (var u in unif[2]) {
				ret = ret + this.uniform_to_string(name, u)
			}
		} 
		else 
		{
			if (unif[1] == "float" or unif[1] == "int") {
				ret = "%(name).set_uniform(\"%(unif[0])\", [ %(unif[2][0]) ])\n"
			} else if (unif[1] == "float2" or unif[1] == "int2") {
				ret = "%(name).set_uniform(\"%(unif[0])\", [ %(unif[2][0]), %(unif[2][1]) ])\n"
			} else if (unif[1] == "float3" or unif[1] == "int3") {
				ret = "%(name).set_uniform(\"%(unif[0])\", [ %(unif[2][0]), %(unif[2][1]), %(unif[2][2]) ])\n"
			} else if (unif[1] == "float4" or unif[1] == "int4") {
				ret = "%(name).set_uniform(\"%(unif[0])\", [ %(unif[2][0]), %(unif[2][1]), %(unif[2][2]), %(unif[2][3]) ])\n"
			}
		}

		return ret
	}

	to_string(name) 
	{
		var code = "
%(name).vs = \"%(this.vs)\"
%(name).tcs = \"%(this.tcs)\"
%(name).tes = \"%(this.tes)\"
%(name).gs = \"%(this.gs)\"
%(name).fs = \"%(this.fs)\"
%(name).cs = \"%(this.cs)\"
%(name).execute()
"
		for (var unif in this.uniforms) {
			code = code + this.uniform_to_string(name, unif)
		}
		return code
	}

	draw_gui(ctx) 
	{
		var dirty = false

		var x = this.pos.x - this.style.width * 0.5
		var y = this.pos.y + this.style.height* 0.5 - this.calc_panel_height() - DEFAULT_HEIGHT

		for (var unif in this.uniforms) {
			var y_d = ShaderUniforms.draw_gui(this, ctx, unif, x, y)
			y = y_d[0]
			if (y_d[1]) {
				dirty = true
			}
		}

		return dirty
	}
}