import "render" for Render
import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.Blueprint" for Blueprint
import "gui" for GUI
import "blueprint.node_layout" for DEFAULT_HEIGHT

class Draw is Node
{
	init()
	{
		super.init(Draw.name)

		this.imports = [
			Pin(this, "prev", "port"),
			Pin(this, "shader", "shader"),
			Pin(this, "va", "va")				
		]
		this.exports = [
			Pin(this, "next", "port")
		]

		// [ "triangles" ]
		this.prim_type = "triangles"

		// { "depth_test" : bool, "cull" : string }
		this.render_state = { 
			"depth_test" : false, 
			"cull" : "disable"
		}

		// for gui
		this.cull_items = ["disable", "front", "back", "front_and_back"]
		this.cull_selected = 0

		this.layout()
	}

	execute() 
	{
		var shader = Blueprint.getInputNode(this, 1)
		var va = Blueprint.getInputNode(this, 2)
		if (shader and shader.prog and va and va.va) {
			shader.updateUniforms()
			Render.draw(this.prim_type, shader.prog, va.va, this.render_state)
		}
	}

	layout() 
	{
		super.layout()

		// depth_test
		this.style.height = this.style.height + DEFAULT_HEIGHT

		// cull
		this.style.height = this.style.height + DEFAULT_HEIGHT
	}

	toString(name) {
		return "
%(name).prim_type = \"%(this.prim_type)\"
%(name).render_state = %(this.render_state)
"
	}

	drawGUI(ctx) 
	{
		var dirty = false

		var x = this.pos.x - this.style.width * 0.5
		var y = this.pos.y + this.style.height* 0.5 - this.calcPanelHeight() - DEFAULT_HEIGHT

		if (this.render_state.containsKey("depth_test"))
		{
			var depth_test = this.render_state["depth_test"]
			var _depth_test = GUI.checkbox(ctx, "depth test", depth_test, x, y)
			if (_depth_test != depth_test) {
				this.render_state["depth_test"] = _depth_test
				dirty = true
			}

			y = y - DEFAULT_HEIGHT
		}

		if (this.render_state.containsKey("cull"))
		{
			var item = GUI.combo(ctx, "cull", this.cull_selected, this.cull_items, x, y, 80)
			if (item != this.cull_selected) {
				this.cull_selected = item
				this.render_state["cull"] = this.cull_items[this.cull_selected]
				dirty = true				
			}

			y = y - DEFAULT_HEIGHT
		}

		return dirty
	}
}