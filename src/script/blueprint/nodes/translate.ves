import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for Variant, VAR_TYPE_ANY, VAR_TYPE_MATRIX4, VAR_TYPE_NUMBER3, VAR_TYPE_ARRAY
import "blueprint.node_layout" for DEFAULT_HEIGHT
import "blueprint.blueprint" for Blueprint
import "gui" for GUI
import "maths.vector" for Vector3
import "maths" for Matrix44

class Translate is Node
{
	init()
	{
		super.init(Translate.name)

		this.imports = [
			Pin(this, "mat", VAR_TYPE_MATRIX4),
			Pin(this, "offset", [VAR_TYPE_NUMBER3, VAR_TYPE_ARRAY]),
		]
		this.exports = [
			Pin(this, "mat", [VAR_TYPE_MATRIX4, VAR_TYPE_ARRAY]),
		]

		this.layout()

		this.dx = 0
		this.dy = 0
		this.dz = 0
	}

	calc(idx) 
	{
		var v_offset = Blueprint.calc_input_value(this, 1)
		if (v_offset.type == VAR_TYPE_NUMBER3 or (v_offset.type == VAR_TYPE_ANY and v_offset.value is Vector3))
		{
			this.dx = v_offset.value.x
			this.dy = v_offset.value.y
			this.dz = v_offset.value.z

			var v_mat = Blueprint.calc_input_value(this, 0)
			v_mat.value.translate(this.dx, this.dy, this.dz)
			return v_mat
		}
		else if (v_offset.type == VAR_TYPE_ARRAY)
		{
			var ret = []
			for (var offset in v_offset.value)
			{
				var v_mat = Blueprint.calc_input_value(this, 0)
				v_mat.value.translate(offset[0], offset[1], offset[2])
				ret.add(v_mat)
			}
			return Variant(VAR_TYPE_ARRAY, ret)
		}
		else
		{
			var v_mat = Blueprint.calc_input_value(this, 0)
			if (v_mat) {
				v_mat.value.translate(this.dx, this.dy, this.dz)
				return v_mat
			} else {
				var mt = Matrix44.init()
				mt.translate(this.dx, this.dy, this.dz)
				return Variant(VAR_TYPE_MATRIX4, mt)		
			}
		}
	}

	layout()
	{
		super.layout()

		this.style.height = this.style.height + DEFAULT_HEIGHT * 3
	}

	to_string(name) {
		return "
%(name).dx = %(this.dx)
%(name).dy = %(this.dy)
%(name).dz = %(this.dz)
"
	}

	draw_gui(ctx) 
	{
		var dirty = false

		var x = this.pos.x - this.style.width * 0.5
		var y = this.pos.y + this.style.height * 0.5 - this.calcPanelHeight() - DEFAULT_HEIGHT

		var REGION = 100

		var dx = GUI.slider(ctx, "dx", this.dx, x, y, this.style.width - 20, REGION, false)
		if (dx != this.dx) {
			this.dx = dx
			dirty = true
		}
		y = y - DEFAULT_HEIGHT

		var dy = GUI.slider(ctx, "dy", this.dy, x, y, this.style.width - 20, REGION, false)
		if (dy != this.dy) {
			this.dy = dy
			dirty = true
		}
		y = y - DEFAULT_HEIGHT

		var dz = GUI.slider(ctx, "dz", this.dz, x, y, this.style.width - 20, REGION, false)
		if (dz != this.dz) {
			this.dz = dz
			dirty = true
		}
		y = y - DEFAULT_HEIGHT

		return dirty
	}
}