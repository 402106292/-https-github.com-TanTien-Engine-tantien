import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for VAR_TYPE_ANY
import "blueprint.blueprint" for Blueprint
import "blueprint.node_layout" for DEFAULT_HEIGHT
import "blueprint.nodes.store" for Store
import "gui" for GUI

class Load is Node
{
	init() 
	{
		super.init(Load.name)

		this.imports = [
			Pin(this, "var", VAR_TYPE_ANY),
		]
		this.exports = [
			Pin(this, "var", VAR_TYPE_ANY),
		]

		this.layout()

		this.imports[0].hide = true

		this.var_name = "load"
	}

	calc_value(idx)
	{
		return Blueprint.calc_input_value(this, 0)
	}

	calc_inst(idx, ctx) 
	{
		var v = Blueprint.calc_input_inst(this, 0, ctx)
		if (v) {
			return ctx.bb.load(v)
		} else {
			return nil
		}
	}

	shader_gen2(node, idx)
	{
		import "shadergraph.shader_builder" for ShaderBuilder

		var v = ShaderBuilder.gen_child(node, "var")
		if (v) {
			var bb = node.find_value("bb")
			return bb.load(v)
		} else {
			return nil
		}
	}
	
	to_string(name) 
	{
		if (this.var_name == "load")
		{
			var f_node = Blueprint.get_input_node(this, 0)
			if (f_node is Store) {
				this.var_name = f_node.var_name
			}
		}
		if (this.var_name == "load")
		{
			var t_pin = Blueprint.get_first_connect_pin(this.exports[0])
			if (t_pin) {
				this.var_name = t_pin.name
			}
		}

		return "
%(name).var_name = \"%(this.var_name)\"
"
	}

	get_title()
	{
		if (this.var_name == "store") {
			return super.get_title()
		} else {
			return "%(this.var_name)(L)"
		}
	}

	on_selected() 
	{
		this.imports[0].hide = false
	}

	on_deselected() 
	{
		this.imports[0].hide = true
	}
}