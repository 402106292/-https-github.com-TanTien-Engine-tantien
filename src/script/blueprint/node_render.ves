import "graphics" for Painter, Graphics
import "blueprint.node_layout" for TITLE_HEIGHT, PIN_RADIUS
import "blueprint.node_style" for COL_PANEL_BG, TB_TITLE, TB_LEFT, TB_RIGHT
import "blueprint.node_layout" for NodeLayout, PIN_TEXT_OFFSET

class NodeRender
{
	static draw_node(node)
	{
		this.draw_node_panel(node)
		this.draw_node_pins(node)
		this.draw_node_conns(node)
	}

	static draw_node_panel(node)
	{
		var pt = Painter.init()

		var hw = node.style.width * 0.5
		var hh = node.style.height * 0.5
		pt.add_rect_filled([node.pos.x - hw, node.pos.y - hh, node.style.width, node.style.height], COL_PANEL_BG)

		Graphics.draw_painter(pt)

		var str
		if (node.title) {
			str = node.title
		} else {
			str = node.name
		}
		Graphics.draw_text(str, node.pos.x, node.pos.y + hh - TITLE_HEIGHT * 0.5, 0.7, TB_TITLE)
	}

	static draw_node_pins(node)
	{
		for (var pin in node.imports)
		{
			if (!pin.hide) {
				var p_pos = NodeLayout.calc_pin_pos(pin)
				this.draw_pin(p_pos.x, p_pos.y, pin)
			}
		}
		for (var pin in node.exports)
		{
			if (!pin.hide) {
				var p_pos = NodeLayout.calc_pin_pos(pin)
				this.draw_pin(p_pos.x, p_pos.y, pin)
			}
		}
	}

	static draw_node_conns(node)
	{
		var pt = Painter.init()

		for (var o in node.exports) {
			for (var c in o.conns) {
				if (!c.to.node.dummy and (!c.from.hide or !c.to.hide)) {
					this.draw_conn(pt, c)
				}
			}
		}

		Graphics.draw_painter(pt)
	}

	static draw_pin(x, y, pin)
	{
		var pt = Painter.init()
		pt.add_circle(x, y, PIN_RADIUS, pin.color)
		Graphics.draw_painter(pt)

		var dx = 0
		var tb
		if (pin.is_input) {
			dx = PIN_TEXT_OFFSET
			tb = TB_LEFT
		} else {
			dx = -PIN_TEXT_OFFSET
			tb = TB_RIGHT		
		}

		Graphics.draw_text(pin.name, x + dx, y, 0.5, tb)
	}

	static draw_conn(pt, conn)
	{
		pt.add_bezier(conn.curve, conn.from.color, 2)
	}
}