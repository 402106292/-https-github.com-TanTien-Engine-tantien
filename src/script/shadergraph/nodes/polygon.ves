import "shadergraph.node_helper" for NodeHelper
import "blueprint.node" for Node
import "blueprint.node_layout" for DEFAULT_HEIGHT
import "gui" for GUI

var block = "
// proposed solution from
// http://stackoverflow.com/questions/26070410/robust-atany-x-on-glsl-for-converting-xy-coordinate-to-angle
// swaps params when |x| <= |y|
float atan2(float y, float x)
{
    bool s = (abs(x) > abs(y));
    return mix(3.14159265358979/2.0 - atan(x,y), atan(y,x), s);
}

// https://docs.unity3d.com/Packages/com.unity.shadergraph@9.0/manual/Polygon-Node.html
float polygon(vec2 st, float sides, float width, float height)
{
    float pi = 3.14159265359;
    float aWidth = width * cos(pi / sides);
    float aHeight = height * cos(pi / sides);
    vec2 _st = (st * 2 - 1) / vec2(aWidth, aHeight);
    _st.y *= -1;
    float pCoord = atan2(_st.x, _st.y);
    float r = 2 * pi / sides;
    float distance = cos(floor(0.5 + pCoord / r) * r - pCoord) * length(_st);
    return clamp((1 - distance) / fwidth(distance), 0.0, 1.0);
}
"

class Polygon is Node
{
	init() 
	{
		super.init(Polygon.name)

		NodeHelper.init_pins_from_code(this, block, "polygon")

		this.sides = 3
		this.width = 0.5
		this.height = 0.5
	}

	layout() 
	{
		super.layout()

		this.style.height = this.style.height + DEFAULT_HEIGHT * 3
	}

	to_string(name) {
		return "
%(name).sides = %(this.sides)
%(name).width = %(this.width)
%(name).height = %(this.height)
"
	}

	draw_gui(ctx) 
	{
		var dirty = false

		var x = this.pos.x - this.style.width * 0.5
		var y = this.pos.y + this.style.height * 0.5 - this.calc_panel_height() - DEFAULT_HEIGHT

		var sides = GUI.slider(ctx, "sides", this.sides, x, y, this.style.width - 20, 20, false)
		if (sides != this.sides) {
			this.sides = sides
			dirty = true
		}
		y = y - DEFAULT_HEIGHT

		var width = GUI.slider(ctx, "width", this.width, x, y, this.style.width - 20, 1, false)
		if (width != this.width) {
			this.width = width
			dirty = true
		}
		y = y - DEFAULT_HEIGHT

		var height = GUI.slider(ctx, "height", this.height, x, y, this.style.width - 20, 1, false)
		if (height != this.height) {
			this.height = height
			dirty = true
		}
		y = y - DEFAULT_HEIGHT

		return dirty
	}

	gen_shader(gen)
	{
		var code = "#version 330 core\n" + block	
		var lib = gen.add_lib("pixel", code)
		this.func = gen.get_func(lib, 1)
		gen.add_link_decl(this.func, "sg_Polygon", true)
	}

	calc_inst(gen, idx) 
	{
		var func = gen.create_decl_func(this.func)
		gen.add_link_decl(func, "sg_Polygon", false)

		var st = NodeHelper.calc_input_inst(gen, this, "st")
		if (!st) {
			st = gen.op_const_float2(0, 0)
		}
		var sides = NodeHelper.calc_input_inst(gen, this, "sides")
		if (!sides) {
			sides = gen.op_const_float(this.sides)
		}
		var width = NodeHelper.calc_input_inst(gen, this, "width")
		if (!width) {
			width = gen.op_const_float(this.width)
		}
		var height = NodeHelper.calc_input_inst(gen, this, "height")
		if (!height) {
			height = gen.op_const_float(this.height)
		}

		return gen.op_call(func, [st, sides, width, height])
	}
}