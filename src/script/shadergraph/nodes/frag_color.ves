import "shadergraph.node_helper" for NodeHelper
import "blueprint.node" for Node
import "blueprint.pin" for Pin
import "blueprint.variant" for VAR_TYPE_NUMBER, VAR_TYPE_NUMBER2, VAR_TYPE_NUMBER3, VAR_TYPE_NUMBER4

class FragColor is Node
{
	init()
	{
		super.init(FragColor.name)	

		this.imports = [
			Pin(this, "rgba", VAR_TYPE_NUMBER4),
			Pin(this, "rgb",  VAR_TYPE_NUMBER3),
			Pin(this, "rg",   VAR_TYPE_NUMBER2),			
			Pin(this, "grey", VAR_TYPE_NUMBER),			
		]

		this.layout()
	}

	gen_shader(gen)
	{
		gen.import_all()

		var frag_col = gen.add_output("FragColor", "vec4")

		var module = gen.get_main_module()
		var func = gen.get_main_func()
		var rgba = NodeHelper.calc_input_inst(gen, this, "rgba", module, func)
		if (!rgba) 
		{
			var r = nil
			var g = nil
			var b = nil
			var a = gen.const_float(module, 1.0)

			var rgb = NodeHelper.calc_input_inst(gen, this, "rgb", module, func)
			if (rgb) 
			{
				r = gen.compose_extract(func, rgb, 0)
				g = gen.compose_extract(func, rgb, 1)
				b = gen.compose_extract(func, rgb, 2)
			} 
			else 
			{
				var rg = NodeHelper.calc_input_inst(gen, this, "rg", module, func)
				if (rg) 
				{
					r = gen.compose_extract(func, rg, 0)
					g = gen.compose_extract(func, rg, 1)
					b = gen.const_float(module, 0.0)
				} 
				else 
				{
					var grey = NodeHelper.calc_input_inst(gen, this, "grey", module, func)
					if (grey) 
					{
						r = grey
						g = grey
						b = grey
					}
					else
					{
						r = gen.const_float(module, 0.0)
						g = r
						b = r
					}
				}
			}

			rgba = gen.compose_float4(func, r, g, b, a)			
		}
		gen.store(func, frag_col, rgba)

		gen.func_return(func)

		gen.finish_main()
	}
}